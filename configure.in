dnl
dnl Configuration input file for euracom
dnl
dnl Michael Bussmann <bus@fgan.de>
dnl
dnl $Id: configure.in,v 1.3 1999/02/07 10:32:21 bus Exp $
dnl

AC_REVISION($Revision: 1.3 $)
AC_INIT(euracom.c)
AC_CONFIG_HEADER(autoconf.h)
AC_PREFIX_DEFAULT(/usr/local)

dnl Set default variables
dbdir="/opt/postgreSQL"

dnl
dnl Check for programs
dnl
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROG(FALSE, false, /usr/bin/false)
AC_PATH_PROG(TRUE, true, /usr/bin/true)
AC_PATH_PROG(RM, rm, $FALSE)

dnl
dnl What database?
dnl
AC_MSG_CHECKING(for PostgreSQL support)
AC_ARG_WITH(postgres,
[  --with-postgreSQL[=dir]	Use PostgreSQL ],
[ dbdir=$withval ])
AC_MSG_RESULT($dbdir)

dnl
dnl Check for libraries
dnl
AC_CHECK_LIB(pq, crypt)
AC_CHECK_LIB(crypt, crypt)

dnl
dnl Check header files
dnl

dnl
dnl Check for typedefs
dnl

dnl
dnl Check structures
dnl

dnl Check compiler characteristics
AC_C_CONST
dnl AC_C_INLINE
dnl AC_CHAR_UNSIGNED

dnl
dnl Check library functions
dnl
AC_CACHE_CHECK(if sys_errlist is already declared, ac_cv_needs_sys_errlist,
  AC_TRY_COMPILE([
    #include <stdio.h>
], [
    char *s = *(sys_errlist);
],
    ac_cv_needs_sys_errlist="yes",
    ac_cv_needs_sys_errlist="no")
)
if test "$ac_cv_needs_sys_errlist" = "no" ; then
  AC_DEFINE(NEED_SYS_ERRLIST)
fi

AC_REPLACE_FUNCS(strerror)
AC_CHECK_FUNC(getopt_long,
[],
[ LIBOBJS="${LIBOBJS} getopt.o getopt1.o" ]
)

dnl
dnl Check system services
dnl

dnl
dnl Optional features
dnl
AC_MSG_CHECKING(if we're including all debugging code)
AC_ARG_ENABLE(debugging,
[  --enable-debugging		include debugging code (yes)],
[ myres=$enableval ],
[ myres=yes ])
if test "$myres" = "yes"; then
  AC_DEFINE(DEBUG)
fi
AC_MSG_RESULT($myres)

AC_MSG_CHECKING(whether KIT DUMP mode should be enabled)
AC_ARG_ENABLE(kit-dump-mode,
[  --disable-kit-dump-mode	disable support KIT DUMP mode (enabled)],
[ myres=$enableval ],
[ myres=yes ])
if test "$myres" = "yes"; then
  AC_DEFINE(KIT_DUMP_MODE)
fi
AC_MSG_RESULT($myres)

AC_MSG_CHECKING(if we should check for CTS)
AC_ARG_ENABLE(cts-check,
[  --disable-cts-check		don't check for CTS (do check)],
[ myres=$enableval ],
[ myres=yes ])
if test "$myres" = "no"; then
  AC_DEFINE(DONT_CHECK_CTS)
fi
AC_MSG_RESULT($myres)

dnl
dnl Firmware release:
AC_MSG_CHECKING(firmware release)
AC_ARG_WITH(firmware,
[  --with-firmware={1,2,3}		compile for euracom f/w 1.x, 2.x or 3.x (1.x)],
[ 
  case "$withval" in
    1) AC_MSG_RESULT(firmware 1.x)
       AC_DEFINE_UNQUOTED(FIRMWARE_MAJOR, 1)
       ;;
    2) AC_MSG_RESULT(firmware 2.x)
       AC_DEFINE_UNQUOTED(FIRMWARE_MAJOR, 2)
       ;;
    3) AC_MSG_RESULT(firmware 3.x)
       AC_DEFINE_UNQUOTED(FIRMWARE_MAJOR, 3)
       ;;
    *) AC_ERROR(Only firmware releases 1 2 or 3 are supported)
       ;;
  esac
],
[ AC_ERROR(You _must_ specify the firmware major number) ]
)

dnl
dnl Write output file
dnl
AC_OUTPUT(Makefile)
